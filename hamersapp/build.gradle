buildscript {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url "https://maven.google.com" }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath 'com.google.gms:google-services:3.2.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com" }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "nl.ecci.Hamers"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 61
        versionName "2.5.2"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            debuggable true
            resValue "string", "app_name", "Hamers debug"
            resValue "string", "host", "https://staging.zondersikkel.nl/"
            resValue "string", "google_maps_key", "***REMOVED***"
        }
        release {
            debuggable false
            resValue "string", "app_name", "Hamers"
            resValue "string", "host", "https://zondersikkel.nl/"
            resValue "string", "google_maps_key", "***REMOVED***"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }
    lintOptions {
        abortOnError false
    }
    dexOptions {
        javaMaxHeapSize "2g"
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    implementation(
        // Android Support Libraries
        "com.android.support:support-v4:$supportLibVersion",
        "com.android.support:appcompat-v7:$supportLibVersion",
        "com.android.support:recyclerview-v7:$supportLibVersion",
        "com.android.support:cardview-v7:$supportLibVersion",
        "com.android.support:design:$supportLibVersion",
        'com.android.support:multidex:1.0.3',
        // Firebase Cloud Messaging + Google Maps
        "com.google.firebase:firebase-messaging:$firebaseVersion",
        "com.google.android.gms:play-services-maps:$firebaseVersion",
        "com.google.android.gms:play-services-location:$firebaseVersion",
        // Libraries for data fetching/management
        'com.android.volley:volley:1.1.0',
        'com.google.code.gson:gson:2.8.2',
        // Libraries for image displaying
        'com.github.bumptech.glide:glide:4.6.1',
        'com.github.bumptech.glide:volley-integration:4.6.1',
        'com.github.chrisbanes:PhotoView:2.1.3',
        'de.hdodenhof:circleimageview:2.2.0',
        // Other libraries
        'us.feras.mdv:markdownview:1.1.0',
        "com.takisoft.fix:preference-v7:$supportLibVersion.0",
        'commons-codec:commons-codec:1.11', // Needed for DigestUtils.md5
        // Kotlin + Anko
        "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
        "org.jetbrains.anko:anko-sdk19:$ankoVersion",
        "org.jetbrains.anko:anko-support-v4:$ankoVersion",
        "org.jetbrains.anko:anko-appcompat-v7:$ankoVersion"
    )

    // Testing
    // testCompile 'junit:junit:4.12'
}

// This needs to be on the bottom
apply plugin: 'com.google.gms.google-services'
